from '../modules/sys.zpp' import [ Error, Ok, Err ]

test 'and':
  a: u8 = true
  b: u8 = true
  c: u8 = false

  try not expect!(a and b and c)
  try not expect!(a and c)
  try not expect!(b and c)

test 'and2':
  n: u8 = true

  try not expect!(false and n)

test 'array':
  a: [3 x i32] = [1, 2, 3]
  b: *[3 x i32] = &a
  c: *mut i32 = b as *mut i32

  a[0] = 2
  (*b)[1] = 3
  c[2] = 4

  try expect!(a[0] + a[1] + a[2] == 9)

test 'bool':
  x: u8 = true
  y: u8 = false

  if x or y:
    return Ok
  
  if y or x:
    return Ok
  
  return Err

test 'bool2':
  x: u8 = false
  y: u8 = false

  try not expect!(x or y)
  try not expect!(y or x)

test 'chr':
  x: u8 = `a`
  y: i32 = `b`

  try expect!(y - x as i32 == 1)

from '../modules/libc.zpp' import [ malloc, free ]

test 'defer':
  x: *mut u8 = malloc(type_size!(|i32|))
  defer free(x)

  *(x as *mut i32) = 2

  -- in return expression is still available
  -- it means the free is called just before
  -- the terminator (return instruction)
  return expect!(*(x as *i32) == 2)

-- iterative fibonacci
fn ifib(n: u16) -> u64:
  a: u64 = 0
  b: u64 = 1

  for .., n > 0, n -= 1:
    t: u64 = a

    a = b
    b = t + b

  return a

-- recursive fibonacci
fn rfib(n: u16) -> u64:
  return n as u64 if n <= 1 else rfib(n - 1) + rfib(n - 2)

test 'iterative fib + recursive fib':
  return expect!(ifib(20) + rfib(15) == 7375)

test 'forloop':
  i: u32 = 0
  for .., i < 10, i += 1:
    pass
  
  return expect!(i == 10)

test 'floating points + nested if':
  x: f32 = 1.2
  y: i32 = (x + 0.8) as i32
  z: f64 = y as f64 + 0.1
  k: u64 = x as u64 + z as u64
  r: u8 = k == 3

  if x == 1.2:
    if y == 2:
      if z == 2.1:
        if r:
          return Ok
  
  return Err

fn add(|T| a: T, b: T) -> T:
  return a + b

test 'generic function':
  a: u8 = add(|u8| 1, 2)
  b: i32 = add(|i32| 3, 4)

  return expect!(a as i32 + b + 1 == 11)

type Node[T] = (data: T, next: *Node[T])

test 'generic type':
  b: Node[i32] = (data: 2, next: null)
  a: Node[i32] = (data: 1, next: &b)
  
  return expect!(a.data + a.next->data == 3)

from 'libs/module_to_import2.zpp' import [ stdout, stderr, do_something_with_fd ]

test 'global variables':
  do_something_with_fd(stdout)
  do_something_with_fd(stderr)

  return expect!(stdout + stderr == 3)

test 'elif statement':
  argc: u32 = 2

  if argc == 1:
    return Err
  elif argc == 2:
    return Ok
  else:
    return Err

test 'nested if statement':
  argc: u32 = 2

  if argc > 1:
    if argc == 2:
      return Ok
    else:
      return Err
  else:
    return Err

test 'inline if':
  a: u8 = true or false

  return expect!(a)

from '../modules/seq.zpp' import [ String, Array ]

type Vec3 = (x: u64, y: u64, z: u64)

test 'literals of any type':
  ThisIsConst: *u8 = cstr!('this must be a comptime value')
  Size: u64 = 2

  s: *u8 = cstr!('hello world') -- this points to the data segment
  y: String = 'sized literal string' -- this also points to it
  c: u8 = `c`
  i: i32 = -1
  u: u64 = 1
  f: f32 = 3.14
  k: i32 = undefined -- not assigned (potentially any value)
  v: Vec3 = (x: 1, y: 2, z: 3)
  w: Vec3 = v
  a: [Size x i32] = [1, 2] -- static array
  b: Array[i32] = (ptr: &a as *i32, len: 2) -- sized array
  o: Array[i64] = &[1, 2, 3]
  
  s = cstr!('goodbye')
  i = true
  u = `i`
  c = null
  f = false
  f = null

  -- the `undefined` value can also be passed to function calls 
  -- (it`s actually a value coercable to any type)
  x: i32 = add(|i32| 1, undefined)

  if x == 1:
    pass
  else:
    pass
  
  -- we can discard non-void values
  .. = 1

  return expect!(y.len + u == 125)

test 'num and fnum literals':
  x: i32 = 1'000'000
  y: f32 = 1'000'000.2
  
  try expect!(x + y as i32 == 2'000'000)
  try expect!(x as f32 + y == 2'000'000.2)

fn foo(x: i64) -> u8:
  return 12

fn bar() -> Error:
  return Ok

test 'fn ptr':
  x: *fn(i64) -> u8 = fn2ptr!(foo)

  try expect!(invoke!(x, 10) == 12)

test 'fn ptr2':
  y: *fn() -> Error = fn2ptr!(bar)
  try invoke!(y)

test 'not':
  a: u8 = false
  
  try expect!(not a)

test 'not2':
  a: u8 = false

  try not expect!(a)
  
  if not a:
    return Ok
  else:
    return Err

test 'or':
  a: u8 = false
  b: u8 = true
  c: u8 = false

  try not expect!(a or c)
  try expect!(a or b or c)

test 'pointers':
  x: u8 = 0
  y: *mut u8 = &mut x
  z: *u8 = &x
  w: *i32 = &10
  -- k: *mut u8 = &mut 10 -- temporary expression allocation address cannot be mutable

  x = 2
  x += 1
  *y = *z
  -- *z = 1 -- unable to write to unmutable pointer
  
  try expect!(x == 3)
  try expect!(*y == *z)

-- iterative pow
fn ipow(base: u64, exp: u16) -> u64:
  r: u64 = 1

  for .., exp > 0, exp -= 1:
    r *= base

  return r

-- recursive pow
fn rpow(base: u64, exp: u16) -> u64:
  return 1 if exp == 0 else base * rpow(base, exp - 1)

test 'iterative pow + recursive pow':
  return expect!(ipow(10, 2) + rpow(10, 3) == 1100)

test 'ptr array access':
  argv: **u8 = carr!([cstr!('hello'), cstr!('world')])

  try expect!(argv[0][0] == `h`)
  try expect!(argv[1][0] == `w`)

type Couple = (a: u8, b: u8)

test 'ptr array access2':
  a: Couple = (a: 1, b: 2)
  b: Couple = (a: 3, b: 4)

  ab: **Couple = carr!([&a, &b])
  
  try expect!((ab[0]->a + ab[0]->b) * (ab[1]->a + ab[1]->b) == 21)

test 'static array':
  a: [3 x i32] = [1, 2, 3]
  b: *[3 x i32] = &a
  c: *i32 = b as *i32
  
  try expect!(a[0] + (*b)[1] + c[2] == 6)

test 'carray':
  x: *mut u16 = carr_mut!([1, 2, 3])
  x[0] = 2

  try expect!(x[0] + x[1] + x[2] == 7)

test 'str from array of chars':
  x: *u16 = carr!([`a`, `b`, `c`])
  -- using `u16` instead of `u8` to avoid overflow when adding together the chars

  try expect!(x[0] + x[1] + x[2] == `a` + `b` + `c`)

fn hash_string(s: *String) -> u64:
  result: u64 = 0

  for i: u64 = 0, i < s->len, i += 1:
    result += s->ptr[i] as u64
  
  return result

test 'hashing String manually (iterating over String)':
  try expect!(hash_string(&'hello world') == 1116)

test 'cstring indexing':
  x: *u8 = cstr!('abc')

  -- casting each term to avoid overflow
  -- if we do `(x[0] + x[1] + ..) as i32` we have UB
  try expect!(x[0] as i32 + x[1] as i32 + x[2] as i32 == 294)

type Person = (name: *u8, age: u8)

test 'struct constructions and field access':
  person: Person = (name: null, age: 2)
  person2: Person = (name: null, age: 1) -- inferred
  
  try expect!(person.age + person2.age == 3)

test 'struct construction and field access2':
  x: (a: i32, b: (k: i32, w: i32)) = (a: 0, b: (k: 1, w: 2))
  x.a += 1
  x.b.k = 2
  x.b.w *= 3

  try expect!(x.b.k + x.b.w == 8)

type K = (m: i32, n: i32)
type R = (a: i32, b: (k: i32, w: *K))

test 'struct construction and field access3':
  t: K = (m: 2, n: 3)
  x: R = (a: 0, b: (k: 1, w: &t))
  y: *R = &x

  try expect!(y->b.k + (y->b.w->m * y->b.w->n) == 7)

SomeError: Error = 1

test 'must success':
  try Ok

test 'also must success':
  pass

test 'must fail':
  e: Error = SomeError

  try e:
    return Ok
  
  return SomeError

Overflow: Error = 1
Underflow: Error = 2

-- there is a internal function for this (see `add_with_overflow`)
fn add_with_overflow(|T| a: T, b: T, result: *mut T) -> Error:
  *result = a + b

  if a > 0 and b > 0 and *result < 0:
    return Overflow

  if a < 0 and b < 0 and *result > 0:
    return Underflow
  
  -- everything worked
  return Ok

test 'try add_with_overflow (should overflow)':
  try e: Error = add_with_overflow(|i32| 2147483647, 2, out result: i32):
    if e != Overflow and e != Underflow:
      return -1
    
    return Ok
  
  return Err

test 'try try_with_overflow2 (should not overflow)':
  try add_with_overflow(|i32| 1, 2, out result: i32)
  try expect!(result == 3)

test 'undefined literal':
  x: i32 = undefined
  y: String = undefined

test 'while loop':
  i: u32 = 0
  while i < 10:
    i += 1

  while i > 0:
    i -= 1

  try expect!(i == 0)

test 'type size':
  try type_size!(|(a: i32, b: i64)|) != 16
  try type_size!(|(a: i32, b: i64, c: u8)|) != 8 * 3
  try type_size!(|[4 x u8]|) != 4
  try type_size!(|f64|) != 8
  try type_size!(|*f32|) != 8

type Cat = (name: String, color: (r: u8, g: u8, b: u8))
type Dog = (name: String, race_name: String)

type AnimalKind = u8
type Animal = (tag: AnimalKind, data: [cat: Cat, dog: Dog])

AnimalKindCat: AnimalKind = 0
AnimalKindDog: AnimalKind = 1

test 'union':
  animal1: Animal = (tag: AnimalKindCat, data.cat: (name: "animal1", color: undefined))
  animal2: Animal = (tag: AnimalKindDog, data.dog: (name: "animal2", race_name: "huskies"))

  animal1.data.cat.color = (r: 0, g: 0, b: 0)
